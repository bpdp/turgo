apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {

    flatDir {
        dirs 'lib'
    }

    mavenLocal()
    mavenCentral()

    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories

    //maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    //maven { url "http://fortytwo.net/maven2" }

    // maven repo for BigData - as embedded RDF database
    // we are looking for BigData with Blueprints also
    // needed by BigData: nxparser
    maven { url "http://nxparser.googlecode.com/svn/repository" }
    maven { url "http://www.systap.com/maven/releases" }

}

dependencies {

    //  compile => for compile time deps
    //  testCompile => for test time deps
    //  provided => assumed to be provided by users

    // grizzly, I put all http and SPDY/3 components here
    compile 'org.glassfish.grizzly:grizzly-http-all:2.3.17'
    compile 'org.glassfish.grizzly:grizzly-npn-bootstrap:1.1'
    compile 'org.glassfish.grizzly:grizzly-spdy:2.3.17'
    // jersey - RESTful Web API, with grizzly integration
    // // used as the door for communication with software agent
    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.13'

    // only for compilation, for running, put:
    // <dependency>
    //      <groupId>org.glassfish</groupId>
    //      <artifactId>javax.json</artifactId>
    //      <version>1.0.4</version>
    //  </dependency>
    // info: https://jsonp.java.net/download.html


    compile 'javax.json:javax.json-api:1.0'

    compile 'org.thymeleaf:thymeleaf:2.1.3.RELEASE'
    
    // bigdata
    compile 'com.bigdata:bigdata:1.4.0'

    // this one in 'lib' dir
    compile 'alice.tuprolog:tuprolog:2.9.1'

    testCompile 'junit:junit:4.11'

}

test {
    /* Configure which tests are included
    include 'org/foo/**'
    exclude 'org/boo/**'
    */
    maxParallelForks = 5
    maxHeapSize = '1024m'

}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId 'name.bpdp'
        artifactId 'turgo'
        version '0.0.1.Beta1'
        inceptionYear '2014'
        packaging 'jar'
        licenses {
            license {
                name 'Apache License (Version 2.0)'
                url 'http://www.apache.org/licenses/LICENSE-2.0'
                distribution 'repo'
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// copy all needed jar after gradle build
task copyToLib(type: Copy) {
	into "lib"
	from configurations.runtime
}